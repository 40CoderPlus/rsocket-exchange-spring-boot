buildscript {
    ext {
        //@formatter:off
        sbVersion      = '3.0.2'
        junitVersion   = '5.9.0'
        //@formatter:on
    }

    repositories {
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$sbVersion"
        classpath "com.palantir.baseline:gradle-baseline-java:4.185.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.1.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.5.0.2730"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.12.1"
        classpath 'org.owasp:dependency-check-gradle:7.4.4'
    }
}

subprojects {
    group 'com.fortycoderplus.rsocket'
    version "1.0.0"

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${sbVersion}"
        }
    }

    plugins.withType(JavaPlugin) {
        project.sourceCompatibility = JavaVersion.VERSION_17
    }
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }
    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'com.fortycoderplus.rsocket'
                artifactId = project.name
                version = project.version

                from components.java

                pom {
                    name = "RSocket Exchange Spring Boot"
                    description = 'RSocket Exchange Spring Boot'
                    url = 'https://github.com/40coderplus/rsocket-exchange-spring-boot'
                    developers {
                        developer {
                            id = 'Codingendless'
                            name = 'Codingendless'
                            email = 'codingendless@40coderplus.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:40coderplus/rsocket-exchange-spring-boot.git'
                        url = 'https://github.com/40coderplus/rsocket-exchange-spring-boot'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$nexusUsername"
                    password "$nexusPassword"
                }
                def releasesRepoUrl = "https://maven.40coderplus.com/repository/maven-releases/"
                def snapshotsRepoUrl = "https://maven.40coderplus.com/repository/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                allowInsecureProtocol = true
            }
        }
    }
}

apply from: 'baseline.gradle'
apply from: 'sonar.gradle'
